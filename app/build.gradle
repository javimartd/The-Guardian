apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'org.sonarqube'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.javimartd.theguardian"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        pro {
            try {
                /*storeFile = file(project.property("THE_GUARDIAN_STORE_FILE"))
                storePassword = project.property("THE_GUARDIAN_STORE_PASSWORD")
                keyAlias = project.property("THE_GUARDIAN_APP_KEY_ALIAS")
                keyPassword = project.property("THE_GUARDIAN_APP_KEY_PASSWORD")*/
            } catch (e) {
                throw new InvalidUserDataException("You should define THE_GUARDIAN_STORE and THE_GUARDIAN_APP_KEY in gradle.properties. " + e.getMessage())
            }
        }

        pre {
            // ...
        }
        uat {
            // ...
        }
        dev {
            // ...
        }
    }

    flavorDimensions "mode"

    productFlavors {

        dev {
            dimension "mode"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField "String", "THE_GUARDIAN_API_KEY", '"db0d0891-0604-403a-9fad-f8ea5a76dbcb"'

        }
        uat {
            dimension "mode"
            applicationIdSuffix ".uat"
            versionNameSuffix "-uat"
            buildConfigField "String", "THE_GUARDIAN_API_KEY", '"db0d0891-0604-403a-9fad-f8ea5a76dbcb"'
        }
        pre {
            dimension "mode"
            applicationIdSuffix ".pre"
            versionNameSuffix "-pre"
            buildConfigField "String", "THE_GUARDIAN_API_KEY", '"db0d0891-0604-403a-9fad-f8ea5a76dbcb"'
        }
        pro {
            dimension "mode"
            buildConfigField "String", "THE_GUARDIAN_API_KEY", '"db0d0891-0604-403a-9fad-f8ea5a76dbcb"'
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
        release {
            debuggable false
            productFlavors.pro.signingConfig signingConfigs.pro
            productFlavors.pre.signingConfig signingConfigs.pre
            productFlavors.uat.signingConfig signingConfigs.uat
            productFlavors.dev.signingConfig signingConfigs.dev

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayout_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:design:$support_version"

    // lifeCycle
    implementation "android.arch.lifecycle:extensions:$lifeCycle_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    kapt "android.arch.lifecycle:compiler:$lifeCycle_version"

    // anko
    implementation "org.jetbrains.anko:anko-common:$anko_version"

    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // retrofit OkHttp
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // room
    implementation "android.arch.persistence.room:runtime:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"

    // room test helpers
    testImplementation "android.arch.persistence.room:testing:$room_version"

    // Gson
    implementation "com.google.code.gson:gson:$gson_version"

    // images
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Required for local unit tests (JUnit 4 framework)
    testImplementation 'junit:junit:4.12'

    // Required for instrumented tests
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    // mock web server
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServer_version"

    // espresso
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    // mockito
    testImplementation 'org.mockito:mockito-core:2.21.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC2'

}

sonarqube {
    properties {
        property "sonar.projectName", "The Guardian"
        property "sonar.projectKey", project.android.defaultConfig.applicationId
        property "sonar.projectVersion", project.android.defaultConfig.versionCode
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.sourceEncoding", "UTF-8"
        property 'sonar.language', 'kotlin'
        property "sonar.sources", "src/main/java, src/main/res"
        property "sonar.exclusions","build/**,**/*.png"
        property "sonar.import_unknown_files", false
        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
    }
}
